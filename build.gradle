// AndroidProject-Kotlin 版本：v13.1
// 导入配置文件
plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
    id 'com.google.gms.google-services' version '4.3.10' apply false
    id 'io.github.wurensen.android-aspectjx' version '3.3.2' apply false
}

apply from: "configs.gradle"


allprojects {

    tasks.withType(JavaCompile) {
        // 设置全局编码
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        // 设置文档编码
        options {
            encoding 'UTF-8'
            charSet 'UTF-8'
            links 'http://docs.oracle.com/javase/7/docs/api'
        }
    }

    // 将构建文件统一输出到项目根目录下的 build 文件夹
    setBuildDir(new File(rootDir, "build/${path.replaceAll(':', '/')}"))
}

subprojects {
    // 解决依赖冲突，强制使用这个版本的依赖
    project.configurations.all {
        resolutionStrategy.force 'androidx.appcompat:appcompat:1.3.1'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}